#########################################
# Kevin Jaeger
# Exercise 2.9 - Manufacturing Process
# From Deterministic Operations Research by David J. Rader Jr.
# AMPL model by , University of Central Oklahoma
# for MATH 4113/5113 - Operations Research 1 - Fall 2016
#########################################

########################
# SETS                 #
########################

# The set of all products produced
set PRODUCTS;

########################
# PARAMETERS           #
########################

# The revenue generated by selling one ounce of each product (in $ per oz)
param Revenue {PRODUCTS} >= 0;

# The maximum amount of each product that can be sold (in oz)
param MaxDemand {PRODUCTS} >= 0;

# The amount of each product (in oz) that can be made from one pound of raw material
param ProducedFromRaw {PRODUCTS} >= 0;

# The amount (in oz) of product j that can be produced from one ounce of product i
param AmountProduced {PRODUCTS, PRODUCTS} >= 0;

# The cost of converting one ounce of product i into product j
param ConversionCost {PRODUCTS, PRODUCTS} >= 0;

# The labor required to convert one ounce of product i into product j (in man-hours)
param ConversionLabor {PRODUCTS, PRODUCTS} >= 0;

# The cost of raw materials (in $ per lb)
param RawMaterialCost >= 0;

# The amount of time taken to process raw material (in man-hours per lb)
param RawMaterialLabor >= 0;

# The amount of raw material available (in lb)
param RawMaterialAvailable >= 0;

# The amount of labor available (in man-hours)
param LaborAvailable >= 0;

########################
# VARIABLES            #
########################

# The production mix of PRODUCTS (Prod1, Prod2, Prod3)
var ProductSold{PRODUCTS} >= 0;
# The amount of raw material used for production
var RawMaterialUsed >= 0;
# The amoount of pruduct converted to another product
var ProductConvertedToOtherProduct{PRODUCTS, PRODUCTS} >= 0;


########################
# OBJECTIVE            #
########################

# The objective here is to maximize the profit for the company selling a mix of 3 products
# while under the constraints of labor and product maximums
maximize Profit: sum{i in PRODUCTS} ProductSold[i] * Revenue[i]
				- sum{i in PRODUCTS, j in PRODUCTS} ProductConvertedToOtherProduct[i,j] * ConversionCost[i,j]
				-  RawMaterialUsed * RawMaterialCost;


# 
#########################
# CONSTRAINTS           #
#########################

# We must produce no more than 5000 product A, 4000 product B, and 3000 product C.
subject to MaximumProduceRequirements{i in PRODUCTS}: ProductSold[i] <= MaxDemand[i];

# We are limited to 25,000 hours of labor
subject to LaborAvailablity:  RawMaterialUsed * RawMaterialLabor 
							+ sum{i in PRODUCTS, j in PRODUCTS} ConversionLabor[i,j] * ProductConvertedToOtherProduct[i,j] <= LaborAvailable;

# Raw material limit
subject to RawMaterialLimit: RawMaterialUsed <= RawMaterialAvailable;

# Production limit structural constraint on converted products ensuring we don't produce more of product j
# then we produce of product i - the amount converted of product i
subject to StructuralConversionLimit{i in PRODUCTS}: ProductSold[i] + sum{j in PRODUCTS} ProductConvertedToOtherProduct[i,j]
													 <= RawMaterialUsed * ProducedFromRaw[i] 
													 + sum{j in PRODUCTS} ProductConvertedToOtherProduct[j,i] * AmountProduced[j,i];




	
